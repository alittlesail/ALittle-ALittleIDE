
namespace ALittleIDE;

public class IDEControlTree : IDEControlTreeLogic
{
	private ALittle.Linear _body;
	private ALittle.DisplayObject _item_checkbutton;

	public ctor(ALittle.ControlSystem ctrl_sys, IDEControlTreeUserInfo user_info)
	{
		this._head = ctrl_sys.CreateControl{ALittle.DisplayObject}("ide_control_tree_head", this);
		ALittle.DisplayGroup.AddChild(this, this._head);
		
		this._item_button.selected = false;
		this._item_button.group = user_info.group;
		this._item_button.AddEventListener(this, this.HandleLButtonDown);
		this._item_button.AddEventListener(this, this.HandleRButtonDown);
		this._item_button._user_data = this;
	
		// 对数的身体部分进行处理
		this._body = new ALittle.Linear(ctrl_sys);
		this._body.type = ALittle.UIEnumTypes.TYPE_V;
		this._body.x = 8;
		this._body.y = this._head.height;
		this._body.width = this._head.width;
		this._item_checkbutton.selected = true;
		ALittle.DisplayGroup.AddChild(this, this._body);
		this._body.AddEventListener(this, this.HandleChildResize);
	
		// 设置一下拾取属性
		this._pickup_rect = false;
		this._pickup_child = true;
	
		this.fold = false;
		if (user_info.root)
			this._item_button.text = "["..this._user_info.module_name.."] "..this._user_info.name;
		else
			this._item_button.text = this._user_info.name;
		this.Refresh();
	}
	
	private fun HandleLButtonDown(ALittle.UILButtonDownEvent event)
	{
		if (event.count == 1) return;
		this.fold = !this.fold;
	}

	private fun HandleRButtonDown(ALittle.UIRButtonDownEvent event)
	{
        var menu = new AUIPlugin.AUIRightMenu();
		if (this._user_info.root)
			menu.AddItem("添加模块", bind(this.HandleAddModule, this));

        menu.AddItem("新建控件", bind(this.HandleNewControl, this));
        menu.AddItem("继承控件", bind(this.HandleExtendsControl, this));

		var can_remove = this._user_info.root && this._user_info.module_name != g_IDEProject.project.name;
		if (can_remove) menu.AddItem("移除模块", bind(this.HandleRemoveModule, this));
    
		menu.Show();
    }

    private async fun HandleAddModule()
	{
		var x, y = this._head.LocalToGlobal();
        var name = g_AUITool.ShowRename("", x, y + this._head.height, 200);
		if (name == null || name == "") return;
		g_IDECenter.center.control_list.AddModule(name);
    }

	private async fun HandleRemoveModule()
	{
        var ui_manager = g_IDEProject.GetUIManager(null);
        if (ui_manager == null) return;

        // 检查是否有当前模块正在编辑。如果有并且已保存那么就全部移除掉
        var remove_list = new List<IDEUITabChild>();
        var tab_child_map = g_IDECenter.center.content_edit.GetTabIdMap{IDEUITabChild}();
        for (var id, tab_child in tab_child_map)
        {
            if (tab_child.module == this._user_info.module_name)
            {
                if (!tab_child.save)
                {
                    g_AUITool.ShowNotice("错误", "还有UI正在编辑并为保存");
                    return;
                }
                ALittle.List_Push(remove_list, tab_child);
            }
        }
        for (var index, tab_child in remove_list)
            g_IDECenter.center.content_edit.CloseTabByName{IDEUITabChild}(tab_child.name);

		var file_name = ALittle.File_GetFileNameByPath(this._user_info.path);
		var result = g_AUITool.DeleteNotice("移除", "确定要移除"..file_name.."模块吗?");
		if (result != AUIPlugin.AUIToolOption.YES) return;
		this.OnDelete();
        this.RemoveFromParent();

		var module_map = g_IDEProject.project.config.GetConfig("control_module", new Map<string, IDEControlModuleInfo>());
        module_map[this._user_info.module_name] = null;
        g_IDEProject.project.config.SetConfig("control_module", module_map);

        g_IDEProject.project.ui[this._user_info.module_name] = null;
        ui_manager.control.UnRegisterPlugin(this._user_info.module_name);
    }

    private fun HandleNewControl()
    {
        g_IDECenter.center.control_list.ShowNewControl(this._user_info.module_name);
    }

    private fun HandleExtendsControl()
    {
        g_IDECenter.center.control_list.ShowExtendsControl(this._user_info.module_name);
    }
	
	public get is_tree() : bool { return true; }

	// 刷新子节点
	public fun Refresh()
	{
		var map = ALittle.File_GetNameListByDir(this._user_info.path);

		// 刷新已存在
		var remove:List<IDEControlTreeLogic> = null;
		for (var index, child:IDEControlTreeLogic in this.childs)
		{
            var attr = map[child._user_info.name];
			if (attr == null)
			{
				if (remove == null) remove = new List<IDEControlTreeLogic>();
				ALittle.List_Push(remove, child);
            }

			map[child._user_info.name] = null;
			child.Refresh();
        }

		if (remove != null)
		{
            for (var index, child:IDEControlTreeLogic in remove)
                this.RemoveChild(child);
        }

        // 添加
		var add_file:List<string> = null;
		var add_dir:List<string> = null;
		for (var name, attr in map)
		{
			if (attr.directory)
			{
                if (add_dir == null) add_dir = new List<string>();
                ALittle.List_Push(add_dir, name);
            }
			else
			{
                if (add_file == null) add_file = new List<string>();
                ALittle.List_Push(add_file, name);
            }	
		}

		if (add_dir != null)
		{
			ALittle.List_Sort(add_dir);
			for (var index, name in add_dir)
			{
                var attr = map[name];
				var info = new IDEControlTreeUserInfo();
				info.module_name = this._user_info.module_name;
				info.module_path = this._user_info.module_path;
				info.name = name;
				info.path = this._user_info.path.."/"..name;
				info.group = this._user_info.group;
				info.root = false;
                info.ui = this._user_info.ui;
                this.AddChild(new IDEControlTree(this._ctrl_sys, info));
            }
        }

		if (add_file != null)
		{
			ALittle.List_Sort(add_file);
			for (var index, name in add_file)
			{
                var attr = map[name];
				var info = new IDEControlTreeUserInfo();
				info.module_name = this._user_info.module_name;
				info.module_path = this._user_info.module_path;
				info.name = ALittle.File_GetJustFileNameByPath(name);
				info.path = this._user_info.path.."/"..name;
				info.group = this._user_info.group;
				info.root = false;
                info.ui = this._user_info.ui;
                this.AddChild(new IDEControlTreeItem(this._ctrl_sys, info));
            }
        }

		this.DispatchEvent(new ALittle.UIResizeEvent());
    }

	// 搜索文件
	public fun SearchFile(string name, [Nullable] List<IDEControlTreeLogic> list) : List<IDEControlTreeLogic>
	{
		if (list == null) list = new List<IDEControlTreeLogic>();
		
		for (var k, child:IDEControlTreeLogic in this._body.childs)
			child.SearchFile(name, list);
	
		return list;
	}

	// 查找文件
	public fun FindFile(string full_path) : IDEControlTreeLogic
	{
		if (ALittle.String_Find(full_path, this._user_info.path) != 1) return null;

		for (var k, child:IDEControlTreeLogic in this._body.childs)
		{
			var item = child.FindFile(full_path);
			if (item != null) return item;
        }
		return null;
    }

	// 触发删除
	public fun OnDelete()
	{
		for (var index, child:IDECodeTreeLogic in this.childs)
            child.OnDelete();
    }
	
	public fun HandleChildResize(ALittle.UIResizeEvent event)
	{
		this.DispatchEvent(new ALittle.UIResizeEvent());
	}
	
	public fun HandleHeadChanged(ALittle.UIEvent event)
	{
		this._body.visible = event.target.selected;
		this.DispatchEvent(new ALittle.UIResizeEvent());
	}
	//////////////////////////////////////////////////////////////////////////////////////
	public fun GetChildIndex(ALittle.DisplayObject child) : int	{ return this._body.GetChildIndex(child);	}
	public fun SetChildIndex(ALittle.DisplayObject child, int index) : bool { return this._body.SetChildIndex(child, index); }
	public fun GetChildByIndex(int index) : ALittle.DisplayObject {	return this._body.GetChildByIndex(index); }
	public fun GetChildIndex(ALittle.DisplayObject child) : int	{ return this._body.GetChildIndex(child);	}
	public get childs() : List<ALittle.DisplayObject> { return this._body.childs; }
	public get child_count() : int { return this._body.child_count;	}
	public fun HasChild(ALittle.DisplayObject child) : bool { return this._body.HasChild(child); }

	public fun AddChild(ALittle.DisplayObject child, [Nullable] int index) : bool
	{
		if (this._body.AddChild(child, index) == false)
			return false;
		child._logic_parent = this;

		if (this._body.abs_visible)
			this.DispatchEvent(new ALittle.UIResizeEvent());
		return true;
	}
	
	public fun RemoveChild(ALittle.DisplayObject child) : bool
	{
		if (this._body.RemoveChild(child) == false)
			return false;
		child.group = null;
		if (this._body.abs_visible)
			this.DispatchEvent(new ALittle.UIResizeEvent());
		return true;
	}

	public fun SpliceChild(int index, [Nullable] int count) : int
	{
        var remain_count = this._child_count - index + 1;
        if (count == null) count = remain_count;
        elseif (count > remain_count) count = remain_count;
        if (count <= 0) return 0;

        var endv = index + count;
        for (var i = index; i < endv; 1)
        {
            var child = this._childs[i];
            if (child == null) break;

			child.group = null;
        }

		var result = this._body.SpliceChild(index, count);
		if (result == 0) return 0;
		if (this._body.abs_visible)
			this.DispatchEvent(new ALittle.UIResizeEvent());
		return result;
    }
	
	public fun RemoveAllChild()
	{
		for (var index, child in this.childs)
			child.group = null;
		this._body.RemoveAllChild();
		
		if (this._body.abs_visible)
			this.DispatchEvent(new ALittle.UIResizeEvent());
	}
	
	//////////////////////////////////////////////////////////////////////
	public get width() : double
	{
		var head_width = 0.0;
		if (this._head != null) head_width = this._head.width;
		var body_width = 0.0;
		if (this._body.visible)
		{
			var start_x = this._body.x;
			body_width = start_x + this._body.width;
			var childs = this._body.childs;
			for (var k, v in childs)
			{
				var child_width = start_x + v.x + v.width;
				if (body_width < child_width) body_width = child_width;
			}
		}
		if (head_width > body_width) return head_width;
		return body_width;
	}
	
	public get height() : double
	{
		var head_height = 0.0;
		if (this._head != null) head_height = this._head.height;
		if (this._body.visible) return head_height + this._body.height;
		return head_height;
	}
	
	public get fold() : bool { return this._body.visible; }
	public set fold(bool value)
	{
		if (value == this._body.visible) return;
	
		this._body.visible = value;
		this._item_checkbutton.selected = value;
		this.DispatchEvent(new ALittle.UIResizeEvent());
	}
	
	public get max_right() : double	{ return this.width; }
	public get max_bottom() : double { return this.height; }
}
