
namespace ALittleIDE;

public class IDEProjectSettingDialog
{
	private ALittle.DisplayObject _project_setting_dialog;	// 项目设置对话框
	private ALittle.DisplayObject _default_show_width;		// 界面宽
	private ALittle.DisplayObject _default_show_height;		// 界面高
	private ALittle.DisplayObject _default_font_path;		// 字体路径
	private ALittle.DisplayObject _default_font_size;		// 字体大小

	public fun ShowSettingProject()
	{
		if (g_IDEProject.project == null)
		{
			g_AUITool.ShowNotice("错误", "当前没有打开的项目");
			return;
		}
        
            // 创建对话框
		if (this._project_setting_dialog == null)
		{
			this._project_setting_dialog = g_Control.CreateControl{ALittle.DisplayObject}("ide_setting_project_dialog", this);
			A_LayerManager.AddToModal(this._project_setting_dialog);
		}
        
            // 显示对话框
		this._project_setting_dialog.visible = true;
	
		// 初始化信息
		this._default_show_width.text = g_IDEProject.project.config.GetConfig("default_show_width", 800);
		this._default_show_height.text = g_IDEProject.project.config.GetConfig("default_show_height", 600);
		this._default_font_path.text = g_IDEProject.project.config.GetConfig("default_font_path", "");
		this._default_font_size.text = g_IDEProject.project.config.GetConfig("default_font_size", 15);
	}
	
	public fun HandleSettingProjectCancel(ALittle.UIEvent event)
	{
		this._project_setting_dialog.visible = false;
	}
	
	public fun HandleSettingProjectConfirm(ALittle.UIEvent event)
	{
		var default_show_width = ALittle.Math_ToInt(this._default_show_width.text);
		if (default_show_width == null || default_show_width <= 0)
		{
			g_AUITool.ShowNotice("错误", "默认宽度不合法");
			return;
		}
		var default_show_height = ALittle.Math_ToInt(this._default_show_height.text);
		if (default_show_height == null || default_show_height <= 0)
		{
			g_AUITool.ShowNotice("错误", "默认高度不合法");
			return;
		}
		var default_font_path = this._default_font_path.text;
		var default_font_size = ALittle.Math_ToInt(this._default_font_size.text);
		if (default_font_size == null || default_font_size <= 0)
		{
			g_AUITool.ShowNotice("错误", "默认字体大小不合法");
			return;
		}
		
		g_IDEProject.project.config.SetConfig("default_show_width", default_show_width);
		g_IDEProject.project.config.SetConfig("default_show_height", default_show_height);
		g_IDEProject.project.config.SetConfig("default_font_path", default_font_path);
		g_IDEProject.project.config.SetConfig("default_font_size", default_font_size);
		
		var changed_event = new IDEProjectSettingChanged();
		changed_event.default_show_width = default_show_width;
		changed_event.default_show_height = default_show_height;
		changed_event.default_font_path = default_font_path;
		changed_event.default_font_size = default_font_size;
		g_IDEProject.DispatchEvent(changed_event);
	
		this._project_setting_dialog.visible = false;
	}
}

protected var g_IDEProjectSettingDialog = new IDEProjectSettingDialog();